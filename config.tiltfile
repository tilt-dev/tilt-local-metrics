# -*- mode: Python -*-

def configmap_yaml(name, namespace="", from_file=None, configmap_type=None):
  """Returns YAML for a generic configmap

  Args:
    name: The configmap name.
    namespace: The namespace.
    from_file: Use the from-file configmap generator. May be a string or a list of strings.
       Example: ["grafana.ini=path/to/grafana.ini"]

  Returns:
    The configmap YAML as a blob
  """

  args = [
    "kubectl",
    "create",
    "configmap",
    name,
  ]

  if namespace:
    args.extend(["-n", namespace])

  generator = False
  if from_file:
    if type(from_file) == "string":
      args.extend(["--from-file", from_file])
      generator = True
    elif type(from_file) == "list":
      for f in from_file:
        args.extend(["--from-file", f])
      generator = True
    else:
      fail("Bad from_file argument: %s" % from_file)

  if not generator:
    fail("No configmap generator specified")

  args.extend(["-o=yaml", "--dry-run=client"])
  return local(args, quiet=True)

def configmap_create(name, namespace="", from_file=None, configmap_type=None):
  """Creates a configmap in the current Kubernetes cluster.

  Args:
    name: The configmap name.
    namespace: The namespace.
    from_file: Use the from-file configmap generator. May be a string or a list of strings.
      Example: ["ssh--privatekey=path/to/id_rsa", "ssh-publickey=path/to/id_rsa.pub"]
    configmap_type (optional): Specify the type of the configmap
      Example: 'kubernetes.io/dockerconfigjson'
  """
  k8s_yaml(configmap_yaml(name, namespace, from_file, configmap_type))
